:start
m=1000
t=3
pln Приключения Сары Фаерман: Мрачная тайна Катакомб Мрака
btn 1, Начать игру
end


:1
t=3
bombset=0
timer=0
boo=0
invkill
inv+ Святая вода
inv+ Палец святого Амвросия
inv+ Священная бомба
inv+ Святые пистолеты
inv+ Серебряный нож
inv+ Осиновый арбалет
;inv+ Боевые четки
inv+ Факел

pln  На многие мили простирались под Черным Городом древние катакомбы. Древнее зло
_ водилось в них, выходя наружу лишь ночью, и повергая в ужас всех обитателей
_ мегаполиса. За ужас и зло, исходящее из них, катакомбы назвали Катакомбами
_ Мрака. И никто не мог остановить террор, в который повергли Город ужасные твари
_ из катакомб и их хозяин, вампир Арман.
pln  И правителями города стали бы вампиры и их миньоны, если бы не она, луч света
_ в царстве мрака - охотница на вампиров, монашка, и просто красивая женщина - 
_ Сара Фаерман. Вооруженная простым кинжалом, бомбой, святой водой, пистолетами,
_ осиновым арбалетом и прочее-прочее, она бесстрашно отворила дверь в катакомбы
_ и ступила вниз, во тьму.
btn 2, Дальше
end

:2
pln  Со скрипом за Сарой Фаерман хлопнула дверь, и чей-то голос за дверью злобно
_ захохотал.
play door1.wav
pause 1000
play door2.wav
pause 1000
play laugh1.wav
pln  - Ты не сможешь выйти из Катакомб Мрака, Сара Фаерман, - сказал голос. - Потому
_ что я запер единственную дверь на засов. А путь к другому входу преграждает
_ #m$ миньонов тьмы.
pln  - Чтож, - бесстрашно ответила Сара Фаерман. - Тогда я уничтожу #m$ миньонов
_ тьмы и выйду через другой вход.
pln  Сара Фаерман обернулась и увидела #(m*2)$ глаз миньонов тьмы, жаждущих пожрать
_ ее хрупкое девичье тело, выпить ее горящую кровь, и погубить ее бессмертную
_ душу.
common_on=1
btn 3, Дальше
end

:3
instr s=  Сара Фаерман оказалась у двери ведущей из катакомб. 
l=4
r=5
d=3
goto commontail
end

:4
play water1.wav
instr s=  Сара Фаерман отметила, что потолок в этом месте течет. Видимо именно здесь,
_ над Катакомбами Мрака, наверху протекала Черная Река.
l=6
r=3
d=4
goto commontail
end

:5
instr s=  Сара Фаерман не заметила в этом мрачном коридоре ничего необычного.
l=3
r=6
d=5
goto commontail
end

:6
instr s= Небольшое вентиляционное отверстие уводит вверх, на поверхность.
l=5
r=4
d=6
goto commontail
end

:commontail
;pln #d$
if common_on=0 then end
if m<1 then goto win2
t=t-1
if Бомба с горящим фитилем and timer<0 then goto death3&end
if bombset=1 and not Бомба с горящим фитилем and timer<0 and boo=d and d<>0 then goto death4&end
if bombset=1 and not Бомба с горящим фитилем and timer<0 and boo<>4 and d<>0 then goto boom&end
if bombset=1 and not Бомба с горящим фитилем and timer<0 and boo=4 and d<>6 then goto death5&end
if bombset=1 and not Бомба с горящим фитилем and timer<0 and boo=4 and d=6 then goto win1&end
if t=-2 then common_on=0&goto death1&end
;if Бомба с горящим фитилем then timer=timer-1
if bombset=1 then timer=timer-1
pln  #m$ миньонов смотрят горящими глазами на Сару Фаерман и тянут к ней свои
_ длинные темные пальцы, жаждя причинить ей вред.
pln Коридоры мрака ведут налево и направо.
pln #%s$
if Бомба с горящим фитилем and bombset=1 and timer>0 then pln  В руках Сары тлеет фитиль бомбы. До конца фитиля, осталось
_ #timer$ дюймов.

common_on=0

if Святая Вода then btn Святая Вода, Святая Вода
if Священная бомба then btn Священная бомба, Священная бомба
if Бомба с горящим фитилем then btn Бомба с горящим фитилем, Бомба с горящим фитилем
if Святые пистолеты then btn Святые пистолеты, Святые пистолеты
if Палец святого Амвросия then btn Палец святого Амвросия, Палец святого Амвросия
if Серебряный нож then btn Серебряный нож, Серебряный нож
if Осиновый арбалет then btn Осиновый арбалет, Осиновый арбалет
if Боевые четки then btn Боевые четки, Боевые четки
if Факел then btn Факел, Факел
end
:move
pln #%s$
if l>0 then common_on=0&btn left, Налево 
if r>0 then common_on=0&btn right, Направо
if l>0 and r>0 then btn there, Стоять на месте
end

:there
common_on=1
l=0
r=0
pln  Сара Фаерман стоит на месте.
btn #d$, Дальше
end

:left
d=l
l=0
r=0
common_on=1
pln  Сара Фаерман идет налево.
btn #d$, Дальше
end

:right
d=r
l=0
r=0
common_on=1
pln  Сара Фаерман идет направо.
btn #d$, Дальше
end

:blocked
pln  #m$ миньонов тьмы блокируют путь.
end

:death1
play yell.mp3
pln  #m$ тварей вонзили свои зубы и пальцы в юное тело Сары Фаерман, разрывая его
_ на части.
pln  Приключения Сары Фаерман на этом закончились.
pln
pln  КОНЦОВКА 1: СОЖРАНА ТВАРЯМИ                                  
goto relive
end

:death3
play explosion.wav
pln  Неожиданно догорел фитиль, и в руках у Сары Фаерман взорвалась бомба.
pln  Приключения Сары Фаерман на этом закончились.
pln
pln  КОНЦОВКА 3: КОРОТКИЙ ФИТИЛЬ
goto relive
end

:death4
play explosion.wav
pln  Неожиданно догорел фитиль и на земле взорвалась бомба. Она унесла жизни
_ не менее двум десяткам миньонов тьмы, но осколок шрапнели вонзился под сердце
_ и Саре Фаерман.
pln  Приключения Сары Фаерман на этом закончились.
pln
pln  КОНЦОВКА 4: ОСКОЛОК ПОД СЕРДЦЕ
goto relive
end

:death5
common_on=0
play gunshot.wav
play flush.mp3
pln  Бомба взорвалась под протекающим потолком, и воды Черной Реки хлынули в 
_ Катакомбы Мрака, уничтожая всех миньонов тьмы. Однако вместе с силами зла они
_ унесли и жизнь Сары, которая плохо умела плавать.
pln  Сара Фаерман победила! Приключения Сары Фаерман на этом закончились.
pln
pln  КОНЦОВКА 5/ПОБЕДА 1: КАМИКАДЗЕ
btn finish, Дальше
end

:win1
common_on=0
play explosion.wav
play flush.mp3
pln  Бомба взорвалась под протекающим потолком, и воды Черной Реки хлынули в 
_ Катакомбы Мрака, уничтожая всех миньонов тьмы. Взрывная волна выбросила Сару
_ Фареман в вентиляционное отверстие, выбрасывая ее наружу. Расправив полы своей
_ широкой рясы, Сара Фаерман плавно спланировала на землю.
pln  Сара Фаерман победила! 
pln
pln  ПОБЕДА 2: ЧИСТАЯ ПОБЕДА
btn finish, Дальше
end

:win2
common_on=0
pln  Не зная ни сна и ни отдыха, Сара Фаерман перебила всех миньонов тьмы вручную!
_ Да, она была не слишком умна и догадлива, но достаточно терпелива и трудолюбива!
pln  Сара Фаерман победила! 
pln
pln  ПОБЕДА 3: ТЕРПЕНИЕ И ТРУД
btn finish, Дальше
end


:Святая вода
common_on=1
play splash.mp3
inv- Святая вода
pln  Сара Фаерман бросила в миньонов тьмы склянку святой воды. Трое из них погибли.
m=m-3
t=3
goto move
end

:Святые пистолеты
common_on=1
inv- Святые пистолеты
pln  Сара Фаерман разрядила в миньонов тьмы святые пистолеты, с пулями отлитыми
_ из серебра креста с собора Рима. Оба миньона погибли.
play gunshot.wav
pause 500
play gunshot.wav
m=m-2
t=3
goto move
end

:Палец святого Амвросия
play holy.wav
common_on=1
pln  Сара Фаерман показала миньонам тьмы Палец святого Амвросия. Те в ужасе расступились.
t=3
goto move
end

:Серебряный нож
play knife.wav
m=m-1
pln  Сара Фаерман достала серебряный нож и вонзила в одного из миньонов. Со
_ страшным криком он издох.
t=3
goto move
end

:Осиновый арбалет
play spring.wav
m=m-1
inv- Осиновый арбалет
pln  Сара Фаерман разрядила арбалет в одного из миньонов, и он издох.
t=3
goto move
end

:Факел
play yell.mp3
pln  Сара Фаерман бросила в миньонов тьмы свой факел. Миньон загорелся и издох.
_ В катакомбах воцарилась тьмы. #(m*10)$ когтистых пальцев тут же вонзились в
_ юное тело Сары, разрывая его на части.
pln  Приключения Сары Фаерман на этом закончились.
pln
pln  КОНЦОВКА 2: СМЕРТЬ ВО ТЬМЕ
goto relive
end

:Бомба с горящим фитилем
common_on=1
play dropping.wav
inv- Бомба с горящим фитилем
t=3
boo=d
pln  Сара Фаерман бросила на пол катакомб бомбу с горящим фитилем.
goto move
end

:Священная бомба
play scratch.wav
pln  Сара Фаерман посмотрела на фитиль, и задумалась, какой длины его сделать.
btn t1, 1 дюйм
btn t2, 2 дюйма
btn t3, 3 дюйма
btn t4, 4 дюйма
btn t5, 5 дюймов
btn t6, 6 дюймов
btn t7, 7 дюймов
btn t8, 8 дюймов
btn t9, 9 дюймов
end

:t1
timer=1
goto bomb
:t2
timer=2
goto bomb
:t3
timer=3
goto bomb
:t4
timer=4
goto bomb
:t5
timer=5
goto bomb
:t6
timer=6
goto bomb
:t7
timer=7
goto bomb
:t8
timer=8
goto bomb
:t9
timer=9
goto bomb

:bomb
bombset=1
common_on=1
inv- Священная Бомба
inv+ Бомба с горящим фитилем
play matches.wav
pln  Сара отмерила #timer$ дюйм фитиля и подожгла Священную Бомбу. Теперь в руках у 
_ нее бомба с горящим фитилем.
t=3
goto move
end

:relive
forget_procs
pln  Но Сара Фаерман никогда не сдается!
quit
end

:boom
bombset=0
common_on=1
play explosion.wav
pln  Где-то в коридорах Катакомб Мрака взорвалась бомба, унеся жизни двум десяткам
_ миньонов тьмы.
btn #d$, Дальше
end

:finish
clrscr
cls
pln  В ужасе бежал вампир Арман. В руинах лежали Катакомбы Мрака,
_ мертва была тысяча миньонов.
pln
pln  Но на этом не заканчивалась борьба со злом! За спиной у вампира Армана стоял
_ загадочный Рулевой, отношения которого с Сарой Фаерман следовало прояснить.
_ Отомстить клялся вампир Арман, и новые миньоны тьмы тренировались в Корридорах
_ Боли.
pln
pln Продолжение следует! Ждите следующую часть приключений Сары Фаерман - Сара
_ Фаерман и вампир Арман!
pln Сару ждут:
pln    еще больше врагов (две тысячи миньонов тьмы);
pln    больше видов оружия (если хватит места на экране);
pln    выяснение отношений с загадочным рулевым.
pln
pln Ждите эротические волпейперы с Сарой в разрешении 1280х800 на странице игры
_ на ЛОКе.
pln (c) 2009 Джордж Павленко, конкурсное издание
quit
end
